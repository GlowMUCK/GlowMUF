wh me=Beginning installation of look-show-exits
@prog look-show-exits
1 99999 d
1 i
( look-show-exits: $Date: 2006/07/18 03:07:11 $ $Revision: 1.4 $              )
( Purpose: To display exits in a given room when called by lib-look           )
( Author: Feaelin Moilar AKA Iain E. Davis                                    )
( Contributors:                                                               )
( Dependencies: lib-wordwrap, lib-edit                                        )
( --------------------------------------------------------------------------- )
( Obvious exits lister. Intended for use with lib-look, but will work as an   )
( @succ if preferred. Lists all not-dark exits not regarded as actions.       )
( Actions are any exit that is not linked to a room. To force an action to be )
( displayed, you can @set <action>=_action?:yes                               )
( To force all actions in a given room or area, @set <room>=_showactions?:yes )
( A wrapping point can be set either on the program or in the environment by: )
( @set <program>=_wrap:78, or @set <room>=_wrap:78, etc.                      )
( To change the prefix on the exit list, @set <program>=_prefix:You can go:...)
( This string can contain glow color codes.                                   )
( Additional color customizations can be made:                                )
( @set <program or environment room>=_show-exit-color-1: for the exit name    )
( @set <program or environment>=_show-exit-color-2: bracket color             )
( @set <program or environment>=_show-exit-color-3: dir letter color          )
( --------------------------------------------------------------------------- )
(
  TBD:
       See old "official" one cmd-showexits for other features to incorp.
)
(
  $Log: look-show-exits,v $
  Revision 1.4  2006/07/18 03:07:11  feaelin
  Added color-customization.

  Revision 1.3  2006/07/17 20:38:30  feaelin
  Added support for the _action? property to force an action to be shown.
  Added support for the _showactions? property force actions to be shown.
  Added support for the wrap point to be settable in the environment.
  Added support for legacy [deprecated] properties.
  
  Revision 1.2  2006/07/17 19:37:30  feaelin
  Added exit darking and fixed my testing shim so it isn't there anymore.
  
  Revision 1.1  2006/07/17 19:23:56  feaelin
  Initial code. Basic exit showing fully implemented.
)

$include $lib/strings
$include $lib/wordwrap
$include $lib/edit
 
( Sets up either the configured prefix or the default string ) 
: get-prefix ( -- s )
  prog "_prefix" getpropstr dup "" strcmp not if
    pop "^CYAN^You can go: "
  then
;
 
( fetch custom color if available )
: get-color-1
  prog "_show-exit-color-1" getpropstr
  dup "" strcmp if exit then
  pop
  trigger @ "_show-exit-color-1" envpropstr
  swap pop dup "" strcmp if exit then
  pop
  "^AQUA^"
;
 
( fetch custom color if available )
: get-color-2
  prog "_show-exit-color-2" getpropstr
  dup "" strcmp if exit then
  pop
  trigger @ "_show-exit-color-2" envpropstr
  swap pop dup "" strcmp if exit then
  pop
  "^BROWN^"
;
 
( fetch custom color if available )
: get-color-3
  prog "_show-exit-color-3" getpropstr
  dup "" strcmp if exit then
  pop
  trigger @ "_show-exit-color-3" envpropstr
  swap pop dup "" strcmp if exit then
  pop
  "^YELLOW^"
;
 
( Substitutions to prettify the output )
: do-substitutions ( s -- s )
  "^COLOR-2^<^COLOR-3^" "<" subst
  "^COLOR-2^>" ">" subst
  "^COLOR-2^(^COLOR-3^" "(" subst
  "^COLOR-2^)" ")" subst
  "^COLOR-2^[^COLOR-3^" "[" subst
  "^COLOR-2^]" "]" subst
  "^COLOR-2^{^COLOR-3^" "{" subst
  "^COLOR-2^}" "}" subst
  get-color-2 "^COLOR-2^" subst
  get-color-3 "^COLOR-3^" subst
;
 
: get-prefix-len ( -- i )
  get-prefix ansi_strlen
;
 
( What is our wrap point? )
: get-wrap-length ( -- i )
  prog "_wrap" getprop dup string? if atoi then
  dup 0 = not if exit then
  pop
  trigger @ "_wrap" envprop swap pop dup string? if atoi then
  dup 0 = not if exit then
  pop
  prog "contents/width" getprop dup string? if atoi then
  dup 0 = not if exit then
  pop
  trigger @ "contents/width" envprop swap pop dup string? if atoi then
  dup 0 = if
    pop 78
  then
;
 
( check an action/exit for either being an exit or being ok )
: action_ok? ( d -- i )
  dup getlink room? if pop 1 exit then
  dup "_action?" getpropstr "y" instr if pop 1 exit then
  "_showactions?" envpropstr swap pop "y" instr
;
 
: show-exits ( -- )
  get-prefix
  ( This shouldn't happen, but just in case )
  trigger @ room? not if
    exit
  then
  0 trigger @ exits
  dup ok? if 
    begin
      dup ok? over exit? and over action_ok? and 
      over "DARK" flag? not and if
        swap 1 + 
        dup 1 > if
          3 pick ", " strcat 3 put
        then
        swap
        ( We want only the first name )
        dup name ";" explode over over 2 + 0 swap - rotate popn
        get-color-1 swap strcat
        do-substitutions
        4 rotate swap strcat -3 rotate
      then
      next
    dup ok? not until
    pop
  else
    pop
  then
  0 = if
   pop me @ "^CYAN^You see no obvious way out." ansi_notify
  else
    get-wrap-length swap WRAP-wordwrap
    swap me @ swap ansi_notify
    1 - dup 0 = not if
      begin
        swap
        "" " " get-prefix-len STRfillfield swap strcat
        me @ swap ansi_notify
      1 - dup 0 = until
    then
  then

;
 
: main ( s -- )
  ( I know this is pointless. Its here just in case I need it later )
  dup "(Look)" strcmp not if ( If called by lib-look, pop, if not, pop )
    pop
  else
    pop
  then
  show-exits
;
.
c
q
@register look-show-exits=look/show-exits
@set $look/show-exits=/_docs:@list $look/show-exits=1-1
@set $look/show-exits=/_version:FM$Revision: 1.4 $
wh me=Installation of look-show-exits complete.
