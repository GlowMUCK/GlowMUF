wh me=Beginning installation of cmd-who...
@prog cmd-who
1 99999 d
1 i
( cmd-who: $Date: 2005/04/18 23:48:01 $ $Revision: 1.4 $                     )
( Purpose: A detailed 'who is in the game'                                    )
( Author: Scotfox                                                             )
( Contributors: Feaelin, Foxen?                                               )
( Dependencies: None                                                          )
( --------------------------------------------------------------------------- )
( Shows a list of everybody connected.  By Scotfox.                           )
( Also uses Foxen's cmd-who                                                   )
( --------------------------------------------------------------------------- )
(
  $Log: cmd-who,v $
  Revision 1.4  2005/04/18 23:48:01  feaelin
  Revised colors. They're now in spectrum order.
  Removed cmd-who's dependency on lib-glow-standard
  Updated documentation.
  Updated installer

  Revision 1.3  2004/03/26 03:06:03  feaelin
  Support for the who_hides_dark sysparm added.
)
( --------------------------------------------------------------------------- )

lvar roomstuff
: get-connected-list  ( concount -- namelist total howmanyleft )
    begin dup 0 > while
        dup condbref dup 
        dup "/@/dark" getpropstr tolower "y" 1 strncmp not
        "who_hides_dark" sysparm tolower "y" 1 strncmp not
        and swap location me @ location dbcmp or if
          pop swap 1 - swap
        else
          name    
          rot rot              ( namelist newname total howmanyleft )
        then
        1 - 
    repeat pop
    1 sort
;
 
: glue-together-list  ( namelist gluedstring howmany  -- s)
    begin dup 0 > while
        rot                  ( namelist gluedstring howmany name )
        over 1 = if  "and " swap strcat
            else ", " strcat then
        rot                  ( namelist howmany name gluedstring )
        swap strcat swap     ( namelist gluedstring howmany )
        1 - repeat
    pop
;
 
: show-connected
    concount dup 1 = if
        me @ "^PURPLE^You are the only one connected." ansi_notify exit then
    dup get-connected-list
    1 sort
    dup 0 <= if
        pop
        "^PURPLE^Everyone is in this room." me @ swap ansi_notify exit then
    dup 1 = if
        pop
        "~" "^" subst
        " is the only player connected who's not in this room." strcat
        "^PURPLE^" swap strcat
        me @ swap ansi_notify exit then
    "" swap glue-together-list
    me @ swap
    "~" "^" subst
    " are awake elsewhere in the game." strcat "^PURPLE^" swap strcat ansi_notify
;
 
$define MIN_IDLE 3 (minutes) $enddef
 
: do_and
    dup ", " rinstr
    dup if
        1 + strcut "and " swap
        strcat strcat
    else pop
    then
;
 
: idletime (d -- i)
    descriptors
    begin dup 1 > while rot pop 1 - repeat
    not if -1 exit then
    descrcon conidle
;
 
: who-loop (pupstr idlestr asleepstr awakestr dbref -- pupstr idlestr asleepstr awakestr)
    dup #-1 dbcmp if
        pop do_and
        4 rotate do_and
        4 rotate do_and
        4 rotate do_and
        4 rotate exit
    then
  
    dup player? if
        dup awake? if
            dup idletime dup MIN_IDLE 60 * < if
                pop over if
                    swap ", " strcat swap
                then
                dup name rot swap strcat swap
            else
                60 / intostr "[" swap strcat "m]" strcat
                5 pick if
                    5 rotate ", " strcat -5 rotate
                then
                over name swap strcat 5 rotate swap strcat -4 rotate
            then
        else
            dup "DARK" flag? not if
                3 pick if
                    rot ", " strcat rot rot
                then
                dup name 4 rotate swap strcat rot rot
            then
        then
    else
        dup "_listen" propdir?
        over "_listen" getpropstr or
        over "_puppet?" getpropstr tolower "y" 1 strncmp not or
        if
            5 pick if
                5 rotate ", " strcat -5 rotate
            then
            dup name 6 rotate swap strcat -5 rotate
        then
    then
    next who-loop
;
 
: cmd-who
  "me" match me !
  "" "" "" "" me @ location contents who-loop
  4 rotate dup if
    dup ", and " instr not if
      "^YELLOW^The only puppet here is " swap strcat
    else
      "~" "^" subst
      "^YELLOW^The puppets here are " swap strcat
      "." strcat
    then
    me @ swap ansi_notify
  else
    pop
  then
  dup me @ name strcmp not if
    pop "^GREEN^You are the only one awake in this room."
  else
    "~" "^" subst
    " are awake in this room." strcat "^GREEN^" swap strcat 
  then
  me @ swap ansi_notify
  swap dup if
    dup ", and " instr not if
      "~" "^" subst
      "^CYAN^Only " swap strcat
      " is idle here." strcat
    else
      " are idle in this room." strcat "^CYAN^" swap strcat
    then
    me @ swap ansi_notify
  else
    pop
  then
  me @ location "DARK" flag? not if
    dup not if
      pop "^BLUE^There are no sleepers here."
    else
      dup ", and " instr not if
        "^BLUE^Only " swap strcat
        " is asleep here." strcat
      else
        "~" "^" subst
        "^BLUE^The sleepers here are " swap strcat
        "." strcat
      then
    then
    me @ swap ansi_notify
  else
    pop
  then
;
 
: main
  pop
  cmd-who
  show-connected
;
.
c
q
@register cmd-who=cmd/who
@set $cmd/who=/_/de:Displays who is online and in this room.
@set $cmd/who=L
@set $cmd/who=W3
@set $cmd/who=_version:FM$Revision: 1.4 $
@action who=here=tmp/exit1
@link $tmp/exit1=$cmd/who
@set $tmp/exit1=/_/de:@$cmd/who
@set $tmp/exit1=M3
wh me=Installation of cmd-who complete.
