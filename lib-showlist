wh me=Beginning installation of lib-showlist...
@prog lib-showlist
1 99999 d
1 i
( showlist:  d s -- : $Date: 2005/04/07 12:58:44 $ $Revision: 1.7 $           )
( Purpose: Displays a property list to a player. Deprecated. Use EDIT-display )
( Author: Unknown                                                             )
( Contributors: None                                                          )
( Dependencies: None                                                          )
( --------------------------------------------------------------------------- )
( showlist d s --         Lists list 's' on database item 'd' to player.      )
( --------------------------------------------------------------------------- )
(
  $Log: lib-showlist,v $
  Revision 1.7  2005/04/07 12:58:44  feaelin
  Updated documentation and installer script.

  Revision 1.6  2005/03/21 01:31:43  feaelin
  General maintenance and documentation updates

  Revision 1.5  2005/01/14 16:32:13  feaelin
  Broad sweep to clear up terminology usage in regards to
  database item/object/thing

  Revision 1.4  2000/01/11 15:55:44  feaelin
  I really hate parentheses.

  Revision 1.3  2000/01/11 15:53:22  feaelin
  Documentation update.

  Revision 1.2  1999/02/13 16:37:36  feaelin
  *sigh* Darn nested parentheses.
) 
$define n me @ swap notify $enddef
 
: getlines ( d s -- i )
  ( checks prop# prop and prop/lines, returns biggest line # )
  over over 
  getpropstr atoi
  3 rotate 3 rotate 
  "/lines" strcat
  getpropstr atoi
  over over < if 
    swap 
  then
  pop
;
 
: showlist ( d s -- )
  ( illegal spam prevention )
  "me" match me !
  ( null list )
  dup not if 
    pop pop exit 
  then
  ( get rid of possible / at end ) 
  dup strlen
  1 - strcut
  dup "/" strcmp if
    strcat
  else
    pop
  then
  over over 
  getlines
  ( try a list#, then a _list if not found )
  dup not if
    pop "#" strcat
    over over
    getlines
  then
  dup not if
    pop "_" 
    swap strcat
    over over 
    getlines
  then
  dup not if 
    pop pop pop exit 
  then
  
  ( convert to propdir and show )
  swap "/" strcat swap
  rot rot 1
  ( n d s/ 1 )
  begin 
    4 pick over >= 
  while
    3 pick 
    3 pick 
    3 pick 
    intostr strcat
    getpropstr
    ( don't notify null lines, as we may be past the end of list )
    dup if n else pop then
    1 +
  repeat
  pop pop pop pop
;

public showlist
.
c
q
@register lib-showlist=lib/showlist
@set $lib/showlist=L
@set $lib/showlist=W
@set $lib/showlist=/_defs/.listread:( d s -- ) "$lib/showlist" match call
@set $lib/showlist=/_defs/.showlist:( d s -- ) "$lib/showlist" match call
@set $lib/showlist=/_defs/listread:( d s -- ) "$lib/showlist" match call
@set $lib/showlist=/_defs/showlist:( d s -- ) "$lib/showlist" match call
@set $lib/showlist=/_/de:lib-showlist: @list $lib/showlist=1-8 for documentation.
@set $lib/showlist=/_docs:@list $lib/showlist=1-8
@set $lib/showlist=_lib-version:FM$Revision: 1.7 $
wh me=Installation of lib-showlist complete.
