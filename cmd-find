wh me=Beginning installation of cmd-find...
@prog cmd-find
1 99999 d
1 i
( cmd-find: $Date: 2006/07/18 13:25:23 $ $Revision: 1.11 $                     )
( Author: PakRat                                                              )
( Contributors: Regabyr, Feaelin                                              )
( Dependencies: lib-strings, lib-match                                        )
( --------------------------------------------------------------------------- )
( PakRat's find copyright 1993 V2.1 with new realm and help features.         )
( This requires a W3 to work, since it requires @ prop access.                )
( Set it L, @reg it to cmd/find, @desc it to @$cmd/find -help                 )
(                                                                             )
( To customize the headings, set the _namename, _worldname, _areaname,        )
( and _roomname props, on the program itself.                                 )
(                                                                             )
( To customize the world and land props, set the _prefs/find/worldprop and    )
(  _prefs/find/landprop properties on #0                                      )
(                                                                             )
( Set the ~world prop and the _land prop as appropiate on parent rooms or set )
( their customized equivalents as appropiate                                  )
( I recommend setting ~world and _land prop on #0 to something like *FLOATING*)
( or *PARENTLESS* to more easily detect rooms that haven't been parented at   )
( all.                                                                        )
(                                                                             )
( You can set "_hideprivate?" to yes on the program itself, and any players   )
( set private will not be shown at all on the find list. Or you can set       )
( '_disallowprivate?' to yes on the program to prevent _private from being    )
( used at all.                                                                )
(                                                                             )
( You can set "_hidewizards?" to yes on the program itself, and all wizards   )
( will not be shown on the find list unless they set the  _prefs/find/showme? )
( prop to yes.                                                                )
(                                                                             )
( Setting _column-length-room, _column-length-area,_column-length-world,      )
( _column-length-name on the program will set the maximum width for those     )
( columns.                                                                    )
(                                                                             )
( Setting _mortal-hide-room to yes on the program will hide the room column   )
( from mortals.                                                               )
(                                                                             )
( Setting _mortal-hide-sleeper to yes on the program will disallow mortals    )
( from using 'find' on sleepers.                                              )
( --------------------------------------------------------------------------- )
(
  $Log: cmd-find,v $
  Revision 1.11  2006/07/18 13:25:23  feaelin
  Some basic capitalization.

  Revision 1.10  2006/01/18 01:31:20  feaelin
  Missing delimiter for split.

  Revision 1.9  2005/09/26 19:08:27  feaelin
  Removed dependency on lib-glow-standard.
  Cleaned up 'do-nothing' defines/macros.
  Replaced call to 'unparseobj' to the fancy unparseobj.
  Added support for various features presented by Narnia [see documentation].

  Revision 1.8  2005/09/22 20:30:23  feaelin
  Removed duplicative library functions from lib-glow-standard:
  .debug-on, .debug-off, .debug-line. All are now inserver defines without
  the period.
  Updated muf prgs that used the period versions to use the inserver define.
  
  Revision 1.7  2004/03/26 03:02:28  feaelin
  Header Cleanup
  
  Revision 1.6  2004/02/23 05:29:30  feaelin
  Updated hidewizards feature with 'showme?' prop
   
  Revision 1.5  2004/02/23 05:14:18  feaelin
  Added 'hidewizards' feature
  
  Revision 1.4  2004/02/23 05:04:44  feaelin
  Fixed bugs in the dark support.
  Added "hideprivate" feature.
  
  Revision 1.3  2004/02/14 15:41:51  feaelin
  Added dark support.
  Added customizable headers by adjusting props on the program.
  Added customizable props by adjusting props on #0
)
lvar ftype
lvar showline
lvar dept
lvar total
 
$include $lib/strings
$include $lib/match
$include $lib/unparseobj
  
$define typeprop  "_prefs/find/type" $enddef
$define dndprop   "_prefs/find/dnd"  $enddef
$define allprop   "_prefs/find/all"  $enddef
 
: private?
  prog "_disallowprivate?" getpropstr tolower "y" 1 strncmp not if
    pop 0
  else
    "_private?" getpropstr tolower "y" 1 strncmp not
  then
;
 
: Maxroom
  prog "_column-length-room" getprop
  dup 0 = if
    pop 37
  then
;
 
: Maxname
  prog "_column-length-name" getprop
  dup 0 = if
    pop 13
  then
;
 
: Maxarea
  prog "_column-length-area" getprop
  dup 0 = if
    pop 14
  then
;
 
: Maxworld
  prog "_column-length-world" getprop
  dup 0 = if
    pop 10
  then
;
 
: worldprop
  #0 "_prefs/find/worldprop" getpropstr dup STR-blank? if
    pop "~world"
  then
;
 
: areaprop
  #0 "_prefs/find/landprop" getpropstr dup STR-blank? if
    pop "_land"
  then
;

: namename
  prog "_namename" getpropstr dup STR-blank? if
    pop "NAME"
  then
;

: worldname
  prog "_worldname" getpropstr dup STR-blank? if
    pop "WORLD"
  then
;

: areaname
  prog "_areaname" getpropstr dup STR-blank? if
    pop "AREA"
  then
;

: roomname
  prog "_roomname" getpropstr dup STR-blank? if
    pop "LOCATION"
  then
;

lvar step

: FilterDark ( dn......d1 n - dn........d1 n )
  ( runs through an 'online' list and removes anyone set @/dark:y )
  dup step !
  begin
    dup while ( dn .... d1 n )
    dup 1 + ( dn .. d1 n n' )
    rotate ( dn-1....d1 n dn )
    dup "@/dark" getpropstr tolower "y" 1 strncmp not  ( dn-1...d1 n dn b )
    prog "_hideprivate?" getpropstr tolower "y" 1 strncmp not if 
      over private? or over location private? or
    then
    prog "_hidewizards?" getpropstr tolower "y" 1 strncmp not if 
      over dup "truemage" flag? swap
      "_prefs/find/showme?" getpropstr tolower "y" 1 strncmp not not and or
    then
    if
      pop step @ 1 - step !
    else
      over 1 + 0 swap - rotate ( dn-1 .... d1 dn n )
    then 1 -
  repeat pop
  step @ ( dn....d1 n )
;
  
: ShowLine0 ( s -- )
  ( s --> *Name Area Room )
  1 strcut swap pop
  MaxWorld ansi_strcut swap pop
  MaxArea ansi_strcut swap "^RED^" swap strcat swap
  MaxRoom ansi_strcut swap "^CYAN^" swap strcat swap
  MaxName ansi_strcut swap "^GREEN^" swap strcat swap
  ( area room name * )
  swap strcat " " strcat
  rot strcat " " strcat
  swap strcat me @ swap ansi_notify
;
  
: ShowLine1 ( s -- )
  ( s --> *Name World Room )
  1 strcut swap pop
  MaxWorld ansi_strcut swap "^PURPLE^" swap strcat swap
  MaxArea ansi_strcut swap pop
  MaxRoom ansi_strcut swap "^CYAN^" swap strcat swap
  MaxName ansi_strcut swap "^GREEN^" swap strcat swap
  ( World Room Name * )
  swap strcat " " strcat
  rot strcat " " strcat
  swap strcat me @ swap ansi_notify
;
  
: ShowLine2 ( s -- )
  ( s --> *Name Room )
  1 strcut swap pop
  MaxWorld MaxArea
  + strcut swap pop
  MaxRoom ansi_strcut swap "^CYAN^" swap strcat swap
  MaxName ansi_strcut swap "^GREEN^" swap strcat swap
  ( Room Name * )
  swap strcat " " strcat
  swap strcat me @ swap ansi_notify
;
  
: ShowLine3 ( s -- )
  ( s --> *Name World Area Room )
  1 strcut swap pop
  MaxWorld ansi_strcut swap "^PURPLE^" swap strcat swap
  MaxArea ansi_strcut swap "^RED^" swap strcat swap
  MaxRoom ansI_strcut swap "^CYAN^" swap strcat swap
  MaxName ansi_strcut swap "^GREEN^" swap strcat swap
  ( world area room name * )
  swap strcat " " strcat
  4 rotate strcat " " strcat
  rot strcat " " strcat
  swap strcat me @ swap ansi_notify
;
  
: ShowString ( {s} -- )
       ftype @ 1 = if 'showline1 showline !
  else ftype @ 2 = if 'showline2 showline !
  else ftype @ 3 = if 'showline3 showline !
  else 'showline0 showline !
  then then then
  begin dup while
    swap ShowLine @ execute
    1 -
  repeat pop
;
 
: FilterWorlds ( {d} -- {d} )
  loc @ worldprop envpropstr swap pop
  over ( {d} s i )
  begin dup while
    3 over + pick location dup if
      worldprop envpropstr swap pop
      3 pick stringcmp if
        3 over + rotate pop rot 1 - rot rot
      then
    else pop then 1 -
  repeat pop pop
;
  
: GetLines ( #d n {d} -- {s} )
  dup ( #d n {d} n )
  begin dup while
    over 2 + rotate
    dup name dup " " Maxname STR-fillfield strcat
    over awake? if " " else "z" then strcat 
    over "interactive" flag?
    3 pick "Haven" flag? or
    3 pick dndprop getpropstr tolower "y" 1 strncmp not
    or if "*" else " " then
    strcat
    over private? me @ "mage" flag? not and if
      "" dup " " Maxworld Maxarea + STR-fillfield strcat
      "... is private" dup " " Maxroom STR-fillfield strcat
      strcat
      swap strcat
      "P" swap strcat
    else
      over location private? me @ "mage" flag? not and if
        "" dup " " Maxworld Maxarea + STR-fillfield strcat
        "... private room" dup " " Maxroom STR-fillfield strcat
        strcat
        swap strcat
        "R" swap strcat
      else
        prog "_mortal-hide-room" getpropstr tolower "y" 1 strncmp not not me @
        "mage" flag? or if
          over location me @ over controls me @ "mage" flag? or if 
            UPO-color-unparseobj
            dup ansi_strlen
            Maxroom > if
              Maxroom ansi_strcut pop
            then
          else
            name 
          then
          ( room name )
          dup ansi_strip " " Maxroom STR-fillfield strcat
          swap strcat
        then
        over location dup if
          Areaprop envpropstr ( area name )
        else "" then
        swap pop
        prog "_mortal-hide-room" getpropstr tolower "y" 1 strncmp not not me @
        "mage" flag? or if
          Maxarea
        else
          Maxarea Maxroom +
        then
        dup 3 pick strlen < if
          swap over strcut pop swap
        then
        over swap
        " " swap STR-fillfield strcat
        swap strcat
        over location dup if
          Worldprop envpropstr ( world name )
        else "" then
        swap pop
        dup strlen Maxworld > if
          Maxworld ansi_strcut pop
        then
        dup
        " "
        Maxworld STR-fillfield strcat
        swap strcat
        "X" swap strcat
      then
    then
    swap pop rot rot 1 - 
  repeat 
  pop
;
 
: SSort 3 sort ;
 
: ListAwake ( d -- )
  "" 0 3 pick location contents
  begin dup while
    ( d s i d )
    dup player? not if
      next continue
    then
    dup 5 pick dbcmp
    over awake? not or if
      next continue
    then
    dup "@/dark" getpropstr "y" instr if
      next continue
    then
    dup private? if
      swap 1 + swap
      next continue
    then
    rot over name
    " " swap strcat
    "," strcat
    strcat rot rot
    next
  repeat pop
  ( d s i )
  over over or not if
    pop pop "^YELLOW^No one is with %o."
    pronoun_sub me @ swap ansi_notify exit
  then
  over not over and if
    pop pop "^YELLOW^%S has company."
    pronoun_sub 1 strcut
    swap toupper swap
    strcat me @ swap ansi_notify exit
  then
  swap STR-cleancommas
  dup " and " instr if
    " are" else " is"
  then strcat
  " with " strcat
  "%o" 4 rotate swap
  pronoun_sub strcat
  swap if
   ", along with some other company."
  else "." then strcat "^YELLOW^" swap strcat me @ swap ansi_notify
;
  
: RoomInfo ( d )
  dup awake? not if
    "^YELLOW^%s is asleep."
    pronoun_sub 1 strcut
    swap toupper swap
    strcat me @ swap ansi_notify exit
  then
  dup private? if pop exit then
  dup location private? if pop exit then
  ListAwake
;
  
: FindDbrefList ( #d {d} )
  ( If #d = #-1, it lists the # of players )
  ( If #d = #-2, it lists # of players with ftype 3 )
  ( If #d = something else, it shows who is in the room with the player. )
  dup 2 + pick #-2 dbcmp not if
    me @ typeprop getpropstr atoi
  else 3 then ftype !
  dup dup 2 +
  -1 * rotate
  ( #d n {d} )
  GetLines
  SSort
  namename dup " " maxname STR-fillfield strcat "^FOREST^" swap strcat
  " " strcat
  "  " swap strcat
  ftype @ dup
  0 = not swap
  2 = not and if
    worldname dup " " maxworld STR-fillfield strcat "^VIOLET^" swap strcat
    strcat
    " " strcat
  then
  ftype @ dup
  1 = not swap
  2 = not and if
    areaname dup " " maxarea STR-fillfield strcat "^CRIMSON^" swap strcat
    strcat
    " " strcat
  then
  prog "_mortal-hide-room" getpropstr tolower "y" 1 strncmp not not me @
  "mage" flag? or if
    roomname "^AQUA^" swap strcat strcat
  then
  me @ swap ansi_notify
  ShowString
  dup 0 = if
    pop pop
    "^BLUE^No one findable is awake." me @ swap ansi_notify
  else
    over int 0 >= if ( we are only finding one player )
      pop ( d ) RoomInfo
    else
      intostr "^BLUE^" swap strcat
      " of " strcat
      online me @ "mage" flag? not if FilterDark then
      dup dup 2 + -1 * rotate popn
      swap over intostr strcat
      " player" strcat
      swap 1 = not if "s" strcat then
      " found.   ^YELLOW^*=Don't disturb   z=sleeping  (find -help for help.)"
      strcat me @ swap ansi_notify
      pop
    then
  then
;
public FindDbrefList
(
** Types of lists find can produce: 
0 - oldfind:  z*, Name, Area, Room
1 - newfind:  z*, Name, World, Room
2 - bigfind:  z*, Name, Room
3 - fullfind: Asleep, Dnd, Name, World, Area, Room
4 - RPFind: Asleep, DND, Name, World, Area
)
: Find-Type (s--)
  dup not if
    me @ typeprop getpropstr atoi intostr
    "Your current find listing type is " swap strcat "." strcat me @ swap
    notify
    "Type find #help to see a list of listing types." me @ swap notify
    pop exit
  then
  atoi intostr dup
  me @ typeprop rot 1 addprop
  "Your find listing type is now " swap strcat "." strcat me @ swap notify
  "(If you were mean and didn't use 0-3 it will act like 0.)" me @ swap notify
;
: Find-Dnd (s--)
  dup not if
    me @ dndprop getpropstr tolower "y" 1 strncmp not if "on" else "off" then
    "Your find Do-Not-Disturb light is " swap strcat "." strcat me @ swap
    notify
    pop exit
  then
  dup tolower "y" 1 strncmp not swap "on" stringcmp not or if
    me @ dndprop "yes" 1 addprop
    "Your find Do-Not-Disturb light has been turned on." me @ swap notify
  else
    me @ dndprop remove_prop
    "Your find Do-Not-Disturb light has been shut off." me @ swap notify
  then
;
: Find-All (s--)
  dup not if
    me @ allprop getpropstr tolower "n" 1 strncmp not not if
      "on" else "off" 
    then
    "Your find-all switch is " swap strcat "." strcat me @ swap notify
    pop exit
  then
  dup tolower "y" 1 strncmp not swap "on" stringcmp not or if
    me @ allprop remove_prop
    "Your find-all switch has been turned on." me @ swap notify
  else
    me @ allprop "no" setprop
    "Your find-all switch has been shut off." me @ swap notify
  then
;
: Find-Help (--)
  "Find 2.0FM" "$Revision: 1.11 $" 11 strcut swap pop dup strlen 1 - strcut pop
  " by PakRat" strcat strcat me @ swap notify
  "~~~~~~~~~~~~~~~~~~" me @ swap notify
  "Find lists information about where players are on the game." me @ swap
  notify
  "Typing just 'find' will show all awake players.  Typing" me @ swap notify
  "'find player1 player2 player3 ...' will list where each player is" me @
  swap notify
  "in the list.  Find protects people and rooms set '_private:yes'." me @ swap
  notify
  " " me @ swap notify
  "Find normally only lists people in the same world as you." me @ swap notify
  "To find everyone on the muck, type 'find all'." me @ swap notify
  " " me @ swap notify
  "Find supports 4 different listings for your tastes:" me @ swap notify
  "find #type 0 -- List Player, SubWorld, and Room. (old find)" me @ swap
  notify
  "find #type 1 -- List Player, World, and Room. (new find)" me @ swap notify
  "find #type 2 -- List Player and Room only. (long room names)" me @ swap
  notify
  "find #type 3 -- List Player, World, SubWorld, and Room. (full)" me @ swap
  notify
  " " me @ swap notify
  "find #dnd on  sets your * attribute, also set if you are haven or in" me @
  swap notify
  "the program editor.  find #dnd off  unsets it. (_prefs/find/dnd:yes)" me @
  swap notify
  " " me @ swap notify
  "find #all on  sets it show just typing 'find' shows all people online."
  me @ swap notify
  "Find #all off  unsets it. (_prefs/find/all:yes)" me @ swap notify
  " " me @ swap notify
  "Send comments, improvements, and bug reports to PakRat." me @ swap notify
;
  
: Find-Setup ( s cmd )
  dup "help" stringcmp not if pop pop Find-Help exit then
  dup "type" stringcmp not if pop Find-Type exit then
  dup "dnd"  stringcmp not if pop Find-Dnd exit then
  dup "all"  stringcmp not if pop Find-All exit then
  pop pop "Unknown option, use find -help for help." me @ swap notify
;
   
: Find-Main ( s )
  dup if
    dup 1 strcut pop
    dup "#" strcmp not
    swap "-" strcmp not
    or if
      1 strcut
      swap pop
      " " STR-split swap
      ( s cmd )
      Find-Setup
      exit
    then
    striplead striptail
    " " ", " subst
    " " "," subst
    begin dup "  " instr while
      " " "  " subst
    repeat
    dup " " strcmp not if pop "" then
  then
  dup not if
    me @ allprop getpropstr tolower "n" 1 strncmp not not if
      pop "all"
    then
  then
  dup over "all" stringcmp and if 
    dup " " instr if
      #-1 swap " " explode
      ( sn ... s1 i )
      "" over
      begin ( sn ... s1 i "" x ) dup while
        3 pick 3 + rotate
        dup MATCH-pmatch dup if
          swap pop -4 rotate
        else
          pop " " swap strcat
          "," strcat rot
          swap strcat swap
          rot 1 - rot rot
        then
        1 -
      repeat pop
      dup if
        STR-cleancommas
        "I couldn't find " swap
        strcat "." strcat me @ swap notify
      else pop then
      dup not if pop pop exit then
    else
      MATCH-pmatch dup not if 
        pop "I don't know who that is.  Type find #help for help." 
        me @ swap notify exit 
      then
      dup awake? not
      prog "_mortal-hide-sleeper" getpropstr tolower "y" 1 strncmp not and
      me @ "mage" flag? not and if
        pop 
        "^RED^You cannot use 'find' to locate sleeping players." me @ swap ansi_notify exit
      then
      dup 1 ( d {d} )
    then
  else
    dup if #-2 else #-1 then
    online me @ "mage" flag? not if FilterDark then
    dup 3 + rotate not if FilterWorlds then
    ( #-1 {d} )
  then
  FindDbrefList
;
: dodebug
  depth dept !
  find-main
  depth dept @ 1 - = not if debug-line then
;
.
c
q
@register cmd-find=cmd/find
@set $cmd/find=L
@set $cmd/find=W3
@set $cmd/find=/_/de:@$cmd/find #help
@action find;findall;fin;fi;fa;where;whereis;@where;@whereis;locate=here=tmp/exit1
@link $tmp/exit1=$cmd/find
@set $tmp/exit1=M3
@set $tmp/exit1=/_/de:@$cmd/find #help
@set $cmd/find=_version:2.0FM$Revision: 1.11 $
@set $cmd/find=_docs:@list 1-40
wh me=Installation of cmd-find complete.
