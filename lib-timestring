wh me=Beginning installation of lib-timestring...
@prog lib-timestring
1 99999 d
1 i
( lib-timestring: $Date: 2005/09/26 02:51:05 $ $Revision: 1.6 $               )
( Purpose: Time related words                                                 )
( Author: Unknown                                                             )
( Contributors: None                                                          )
( Dependencies: None                                                          )
( --------------------------------------------------------------------------- )
( TIME-timestring: n -- s  Given a number of seconds 'n', the word returns a  )
(                          string containing the number of days, hours,       )
(                          minutes and seconds                                )
( --------------------------------------------------------------------------- )
(
  $Log: lib-timestring,v $
  Revision 1.6  2005/09/26 02:51:05  feaelin
  Fixed _docs property.

  Revision 1.5  2005/04/08 00:11:20  feaelin
  Script installer update
  Documentation updates
  
  Revision 1.4  2005/03/21 01:31:43  feaelin
  General maintenance and documentation updates
)
( converts a number of seconds into a string containing )
( the number of days, hours, minutes, and seconds. )
: time_string ( n -- s )
    "" swap
    dup 0 < if pop 0 then
    dup 86400 / dup if
        dup intostr " day" strcat
        swap 1 = not if "s" strcat then
        ", " strcat
        rot swap strcat swap
    else pop
    then
    dup 86400 % 3600 / dup if
        dup intostr " hour"  strcat
        swap 1 = not if "s" strcat then
        ", " strcat
        rot swap strcat swap
    else pop
    then
    dup 3600 % 60 / dup if
        dup intostr " minute"  strcat
        swap 1 = not if "s" strcat then
        ", " strcat
        rot swap strcat swap
    else pop
    then
    dup 60 % dup if
        dup intostr " second"  strcat
        swap 1 = not if "s" strcat then
        rot swap strcat swap
    else pop
    then pop
    strip " " "  " subst
    dup dup strlen 1 - strcut
    "," strcmp not if swap then
    pop
;
.
c
q
@register lib-timestring=lib/timestring
@set $lib/timestring=L
@set $lib/timestring=M2
@set $lib/timestring=/_defs/.time_string:"$lib/timestring" match call
@set $lib/timestring=/_defs/.timestring:"$lib/timestring" match call
@set $lib/timestring=/_defs/time_string:"$lib/timestring" match call
@set $lib/timestring=/_defs/timestring:"$lib/timestring" match call
@set $lib/timestring=/_defs/TIME-timestring:"$lib/timestring" match call
@set $lib/timestring=/_lib-version:FM$Revision: 1.6 $
@set $lib/timestring=/_lib-symbol:TIME
@set $lib/timestring=/_docs:@list $lib/timestring=1-9
@set $lib/timestring=/_/de:Use @list $lib/timestring=1-9 for documentation
wh me=Installation of lib-timestring complete.
