wh me=Beginning installation of cmd-3who...
@prog cmd-3who
1 99999 d
1 i
( cmd-3who: $Date: 2014/09/20 02:55:23 $ $Revision: 1.6 $                     )
( Purpose: Provides a who list in 3 column format                             )
( Author: Unknown                                                             )
( Contributors: Feaelin                                                       )
( Dependencies: None                                                          )
( --------------------------------------------------------------------------- )
( If the "who_hides_dark" sysparm is set, players who are set /@/dark:yes     )
( will not be listed by cmd-3who                                              )
( --------------------------------------------------------------------------- )
( 
  $Log: cmd-3who,v $
  Revision 1.6  2014/09/20 02:55:23  feaelin
  Fix the player count includes hidden players.
  Refactored to be more maintainable.

  Revision 1.5  2006/08/15 13:49:08  feaelin
  Made 3who respect the 'who_hides_dark' sysparm.

  Revision 1.4  2005/04/08 18:44:02  feaelin
  Fixed flaws in the dark hiding prop detection.

  Revision 1.3  2005/04/08 18:17:29  feaelin
  Documentation and installer script updates.

  Revision 1.2  2003/01/24 17:05:07  feaelin
  Modified so it recognizes the /@/dark prop

  Revision 1.1  1999/04/17 23:42:43  feaelin
  Initial revision

)
 
: convertIdleTime (i -- s)
    dup 86400 > if
        86400 / intostr "d" strcat 
    else dup 3600 > if
            3600 / intostr "h" strcat
        else dup 60 > if
                60 / intostr "m" strcat
            else
                intostr "s" strcat
            then
        then
    then
    "    " swap strcat
    dup strlen 4 - strcut swap pop
;
 
: convertTimeOn (i -- s)
  "" over 86400 > if
    over 86400 / intostr "d " strcat strcat
    swap 86400 % swap
  then
  over 3600 / intostr
  "00" swap strcat
  dup strlen 2 - strcut
  swap pop strcat ":" strcat
  swap 3600 % 60 / intostr
  "00" swap strcat
  dup strlen 2 - strcut
  swap pop strcat
;
 
: buildName (i -- s)
  condbref name "^GREEN^" swap strcat
;
 
: buildTimeOn (i -- s)
  contime convertTimeOn "^PURPLE^" swap strcat
;
 
: buildIdleTime (i -- s)
  conidle convertIdleTime "^YELLOW^" swap strcat
;
 
: buildEntry (i -- s)
  dup buildName
  over buildTimeOn

  over ansi_strlen over ansi_strlen +
  dup 19 < if
    "                   " (19 spaces)
    swap strcut swap pop
  else
    19 - rot dup ansi_strlen rot -
    strcut pop swap ""
  then
  swap strcat strcat

  swap buildIdleTime strcat
;
 
: playerVisible? ( d -- b )
  "/@/dark" getpropstr "yes" strcmp 
  "who_hides_dark" sysparm "yes" strcmp 
  or
;
 
: getNameList  ( -- {s})
  preempt
  0 concount
  begin
    dup 0 > while
    dup condbref playerVisible? if
      dup buildEntry
      rot 1 + rot
    then
    1 -
  repeat
  pop
  foreground
;
 
lvar col
: showNameList ({s} -- )
    begin
        dup 3 >= while
        swap "   " strcat
        over 3 / 3 pick 3 % 2 + 3 / +
        dup col ! 2 +
        rotate strcat "   " strcat
        over 3 / 3 pick 3 % 1 +
        3 / + col @ + 1 +
        rotate strcat
        me @ swap ansi_notify 3 -
    repeat
    dup if
        ""
        begin
            over 0 > while
            rot strcat "   " strcat
            swap 1 - swap
        repeat
        me @ swap ansi_notify
    then
    pop
;
 
: showHeader ( -- )
  me @ "^FOREST^Name        ^VIOLET^Time On ^BROWN^Idle  " dup dup strcat strcat ansi_notify  
;
 
: storePlayerCount ( {s} -- i {s} )
  dup dup 2 + -1 * rotate
;
 
: showPlayerCount ( i -- )
  intostr "^BLUE^" swap strcat " players are connected." strcat me @ swap ansi_notify
;
 
: main
  pop
  showHeader
  getNameList
  storePlayerCount
  showNameList
  showPlayerCount
;
.
c
q
@register cmd-3who=cmd/3who
@set $cmd/3who=W3
@set $cmd/3who=_version:FM$Revision: 1.6 $
@action 3who;who3;3w=here=tmp/exit1
@link $tmp/exit1=$cmd/3who
@set $tmp/exit1=M3
@set $tmp/exit1=/_/de:Same as the WHO list of everyone connected, except in three columns.
wh me=Installation of cmd-3who complete.
