wh me=Beginning installation of lib-connects...
@prog lib-connects
1 99999 d
1 i
( lib-connects: $Date: 2005/04/07 21:19:47 $ $Revision: 1.5 $                 )
( Purpose: Provides words related to various player connections               )
( Author: Unknown                                                             )
( Contributors: None                                                          )
( Dependencies: None                                                          )
( --------------------------------------------------------------------------- )
( CONN-firstconn  d -- i     Returns d's first connection number              )
( CONN-findconn   a d -- i   Using function at address a for comparison,      )
(                            finds the connection meeting that criteria       )
( CONN-lastconn   d -- i     Returns d's last connection number               )
( CONN-leastidle  d -- i     Returns d's least idle connection                )
( CONN-mostidle   d -- i     Returns d's most idle connection                 )
( --------------------------------------------------------------------------- )
(
  $Log: lib-connects,v $
  Revision 1.5  2005/04/07 21:19:47  feaelin
  Added Library Symbols

  Revision 1.4  2005/04/06 18:27:37  feaelin
  Updated documentation. Fixed library words to be in line with new
  standard.

  Revision 1.3  2005/03/21 01:31:42  feaelin
  General maintenance and documentation updates

  Revision 1.2  2000/01/11 15:36:15  feaelin
  Added documentation.
)
( --------------------------------------------------------------------------- )
: findconn (a d -- i)
  0 swap concount
  begin
    dup while
    dup condbref 3 pick dbcmp if
      3 pick if
 3 pick over 6 pick execute if
   dup 3 put
 then
      else
 dup 3 put
      then
    then
    1 -
  repeat
  pop pop swap pop
;
 
: leastidle-comp conidle swap conidle < ;
 
: leastidle
  (returns the number of the least idle connection for the given player)
  'leastidle-comp swap findconn
;
 
: mostidle-comp conidle swap conidle > ;
 
: mostidle
  (returns the number of the most idle connection for the given player)
  'mostidle-comp swap findconn
;
 
: firstconn (d -- i)
  1 begin
    dup concount <= while
    dup condbref 3 pick dbcmp if swap pop exit then
    1 +
  repeat
  pop pop 0
;
 
: lastconn (d -- i)
  concount begin
    dup while
    dup condbref 3 pick dbcmp if swap pop exit then
    1 -
  repeat
  pop pop 0
;
public findconn
public leastidle
public mostidle
public firstconn
public lastconn
.
c
q
@register lib-connects=lib/connects
@set $lib/connects=L
@set $lib/connects=M3
@set $lib/connects=/_defs/CONN-findconn:"$lib/connects" match "findconn" call
@set $lib/connects=/_defs/CONN-firstconn:"$lib/connects" match "firstconn" call
@set $lib/connects=/_defs/CONN-lastconn:"$lib/connects" match "lastconn" call
@set $lib/connects=/_defs/CONN-leastidle:"$lib/connects" match "leastidle" call
@set $lib/connects=/_defs/CONN-mostidle:"$lib/connects" match "mostidle" call
@set $lib/connects=/_/de:lib-connects: @list $lib/connects=1-10 for documentation.
@set $lib/connects=/_docs:@list $lib/connects=1-10
@set $lib/connects=/_lib-version:FM$Revision: 1.5 $
@set $lib/connects=/_lib-symbol:CONN
wh me=Installation of lib-connects completed.
