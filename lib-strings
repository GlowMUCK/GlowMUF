wh me=Beginning installation of lib-strings...
@prog lib-strings
1 99999 d
1 i
( lib-strings: $Date: 2005/09/25 13:07:10 $ $Revision: 1.21 $                 )
( Purpose: Provides various string cleanup and parsing routines               )
( Author: Unknown                                                             )
( Contributors: Feaelin                                                       )
( Dependencies: None                                                          )
( --------------------------------------------------------------------------- )
( These routines deal with spaces in strings.                                 )
( STR-blank?   [       str -- bool          ]  true if str null or only spaces)
( STR-sms      [       str -- str'          ]  strips mult. internal spaces   )
(                                                                             )
( These two are routines to split a string on a substring, non-inclusively.   )
( STR-split    [ str delim -- prestr postr  ]  splits str on first delim.     )
( STR-rsplit   [ str delim -- prestr postr  ]  splits str on last delim.      )
( STR-splitargs[       str -- str str       ]  split a string at first space  )
(                                              or = sign                      )
(                                                                             )
( The following are useful for formatting strings into fields. As of revision )
( 1.10, they are aware of Glow-ansi-codes, and should pad correctly strings   )
( containing them.                                                            )
( STR-fillfield[ str char width -- padstr   ]  return padding string to width )
(                                              chars                          )
( STR-center   [ str width -- str'          ]  center a string in a field.    )
( STR-left     [ str width -- str'          ]  pad string w/ spaces to width  )
(                                              chars                          )
( STR-right    [ str width -- str'          ]  right justify string to width  )
(                                              chars                          )
( These convert between ascii integers and string character.                  )
( STR-asc      [      char -- i             ]  convert character to ASCII     )
(                                              number                         )
( STR-char     [         i -- char          ]  convert number to character    )
(                                                                             )
( This routine is useful for parsing command line input:                      )
( STR-parse    [      str -- str1 str2 str3 ] " #X Y  y = Z" -> "X" "Y y" " Z")
(                                                                             )
( The following routines are deprecated, do not use them.                     )
( STR-sls      [       str -- str'          ]  use 'striplead'                )
( STR-sts      [       str -- str'          ]  use 'striptail'                )
( STR-strip    [       str -- str'          ]  use 'strip'                    )
( --------------------------------------------------------------------------- )
(
  $Log: lib-strings,v $
  Revision 1.21  2005/09/25 13:07:10  feaelin
  Changelog update

  Revision 1.20  2005/04/07 21:08:58  feaelin
  Library Symbol added
 
  Revision 1.18  2005/04/05 15:31:20  feaelin
  Updated documentation in light of the new library word name standard
 
  Revision 1.17  2005/04/05 15:23:19  feaelin
  Provided more aliases of the library words
 
  Revision 1.16  2005/04/03 15:02:27  feaelin
  Simple whitespace changes.
 
  Revision 1.15  2005/04/03 14:55:01  feaelin
  Fixed installer script
 
  Revision 1.14  2005/03/21 01:31:43  feaelin
  General maintenance and documentation updates
 
  Revision 1.13  2004/08/06 14:12:22  feaelin
  Minor documentation corrections.
 
  Revision 1.12  2004/04/22 17:25:53  feaelin
  Documentation Cleanup
 
  Revision 1.11  2001/02/22 20:46:31  feaelin
  Documentation correction.
   
  Revision 1.10  2001/02/22 20:39:56  feaelin
  Added support to STRfillfield, STRcenter STRleft, STRright to detect
  and handle correctly strings with ANSI codes in them.  Credit goes to
  Syvel for the original concept for this correction.
  
  Revision 1.8  1999/02/13 19:06:17  feaelin
  Fixed .split2 entry
  
  Revision 1.6  1998/07/30 19:20:41  feaelin
  Added synonyms of .split: .splitarg, .splitargs, .splitfirst, .strcutat.
  
  Revision 1.4  1998/07/30 19:14:48  feaelin
  Added STRsplitargs/.splitargs
  
  Revision 1.3  1998/07/30 04:11:20  feaelin
  Updated STRsplit
  
  Revision 1.2  1998/07/28 03:35:38  feaelin
  Corrected wiz-level.
)  
( --------------------------------------------------------------------------- )
: split ( s si -- s s )
   over over instr dup if
      rot swap 1 - strcut rot strlen strcut swap pop
   else pop pop "" then
;
 
: splitargs ( s -- s s )
  dup " " instr if
    dup "=" instr if
      dup " " instr over "=" instr - 0 < if
        " " split
      else "=" split then
    else " " split then
  else "=" split then
;
 
: rsplit
    swap over over swap
    rinstr dup not if
        pop swap pop ""
    else
        1 - strcut rot
        strlen strcut
        swap pop
    then
;
 
: sms ( str -- str')
    dup "  " instr if
        " " "  " subst 'sms jmp
    then
;
 
: fillfield (str padchar fieldwidth -- padstr)
  rot ansi_strlen - dup 1 < if pop pop "" exit then
  swap over begin swap dup strcat swap 2 / dup not until pop
  swap ansi_strcut pop
;
 
: left (str fieldwidth -- str')
  over " " rot fillfield strcat
;
 
: right (str fieldwidth -- str')
  over " " rot fillfield swap strcat
;
 
: center (str fieldwidth -- str')
  over " " rot fillfield
  dup ansi_strlen 2 / ansi_strcut
  rot swap strcat strcat
;
 
: STRasc ( c -- i )
    " !\"#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ"
    "[\\]^_`abcdefghijklmnopqrstuvwxyz{|}~" strcat
    swap
    dup not if
	and exit
    then
    instr dup if
        31 +
    then
;
 
: STRchr ( i -- c )
    dup 31 > over 128 < and if
        " !\"#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ"
        "[\\]^_`abcdefghijklmnopqrstuvwxyz{|}~" strcat
        swap 32 - strcut swap pop 1 strcut pop
    else
        pop "."
    then
;
 
: STRparse ( s -- s1 s2 s3 ) (
    Before: " #option  tom dick  harry = message "
    After:  "option" "tom dick harry" " message "
    )
    "=" rsplit swap
    striplead dup "#" 1 strncmp not if
        1 strcut swap pop
        " " split
    else
        "" swap
    then
    strip sms rot
;
 
public split
public rsplit
public sms
public fillfield
public left
public right
public center
public STRasc
public STRchr
public STRparse
public splitargs
.
c
q
@register lib-strings=lib/strings
@set $lib/strings=L
@set $lib/strings=W
@set $lib/strings=/_defs/.asc:"$lib/strings" match "STRasc" call
@set $lib/strings=/_defs/.blank?:striplead not
@set $lib/strings=/_defs/.center:"$lib/strings" match "center" call
@set $lib/strings=/_defs/.chr:"$lib/strings" match "STRchr" call
@set $lib/strings=/_defs/.command_parse:"$lib/strings" match "STRparse" call
@set $lib/strings=/_defs/.fillfield:"$lib/strings" match "fillfield" call
@set $lib/strings=/_defs/.left:"$lib/strings" match "left" call
@set $lib/strings=/_defs/.right:"$lib/strings" match "right" call
@set $lib/strings=/_defs/.rsplit:"$lib/strings" match "rsplit" call
@set $lib/strings=/_defs/.singlespace:"$lib/strings" match "sms" call
@set $lib/strings=/_defs/.sms:"$lib/strings" match "sms" call
@set $lib/strings=/_defs/.split:"$lib/strings" match "split" call
@set $lib/strings=/_defs/.split2:"$lib/strings" match "split" call
@set $lib/strings=/_defs/.splitarg:"$lib/strings" match "splitargs" call
@set $lib/strings=/_defs/.splitargs:"$lib/strings" match "splitargs" call
@set $lib/strings=/_defs/.splitfirst:"$lib/strings" match "splitargs" call
@set $lib/strings=/_defs/.stripspaces:strip
@set $lib/strings=/_defs/.strcutat:"$lib/strings" match "splitargs" call
@set $lib/strings=/_defs/STRasc:"$lib/strings" match "STRasc" call
@set $lib/strings=/_defs/STRblank?:striplead not
@set $lib/strings=/_defs/STRcenter:"$lib/strings" match "center" call
@set $lib/strings=/_defs/STRchr:"$lib/strings" match "STRchr" call
@set $lib/strings=/_defs/STRfillfield:"$lib/strings" match "fillfield" call
@set $lib/strings=/_defs/STRleft:"$lib/strings" match "left" call
@set $lib/strings=/_defs/STRparse:"$lib/strings" match "STRparse" call
@set $lib/strings=/_defs/STRright:"$lib/strings" match "right" call
@set $lib/strings=/_defs/STRrsplit:"$lib/strings" match "rsplit" call
@set $lib/strings=/_defs/STRsinglespace:"$lib/strings" match "sms" call
@set $lib/strings=/_defs/STRsls:striplead
@set $lib/strings=/_defs/STRsms:"$lib/strings" match "sms" call
@set $lib/strings=/_defs/STRsplit:"$lib/strings" match "split" call
@set $lib/strings=/_defs/STRsplitargs:"$lib/strings" match "splitargs" call
@set $lib/strings=/_defs/STRstrip:strip
@set $lib/strings=/_defs/STRsts:striptail
@set $lib/strings=/_defs/STR-asc:"$lib/strings" match "STRasc" call
@set $lib/strings=/_defs/STR-blank?:striplead not
@set $lib/strings=/_defs/STR-center:"$lib/strings" match "center" call
@set $lib/strings=/_defs/STR-chr:"$lib/strings" match "STRchr" call
@set $lib/strings=/_defs/STR-fillfield:"$lib/strings" match "fillfield" call
@set $lib/strings=/_defs/STR-left:"$lib/strings" match "left" call
@set $lib/strings=/_defs/STR-parse:"$lib/strings" match "STRparse" call
@set $lib/strings=/_defs/STR-right:"$lib/strings" match "right" call
@set $lib/strings=/_defs/STR-rsplit:"$lib/strings" match "rsplit" call
@set $lib/strings=/_defs/STR-singlespace:"$lib/strings" match "sms" call
@set $lib/strings=/_defs/STR-sls:striplead
@set $lib/strings=/_defs/STR-sms:"$lib/strings" match "sms" call
@set $lib/strings=/_defs/STR-split:"$lib/strings" match "split" call
@set $lib/strings=/_defs/STR-splitargs:"$lib/strings" match "splitargs" call
@set $lib/strings=/_defs/STR-strip:strip
@set $lib/strings=/_defs/STR-sts:striptail
@set $lib/strings=/_docs:@list $lib/strings=1-39
@set $lib/strings=/_/de:lib-strings: List lines 1-39 for documentation.
@set $lib/strings=/_lib-version:FM$Revision: 1.21 $
@set $lib/strings=/_lib-symbol:STR
wh me=Installation of lib-strings complete.
